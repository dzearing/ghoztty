const std = @import("std");
const builtin = @import("builtin");
const Allocator = std.mem.Allocator;
const ArenaAllocator = std.heap.ArenaAllocator;
const EnvMap = std.process.EnvMap;
const config = @import("../config.zig");
const homedir = @import("../os/homedir.zig");
const internal_os = @import("../os/main.zig");

const log = std.log.scoped(.shell_integration);

/// Shell types we support
pub const Shell = enum {
    bash,
    /// cmd.exe (T512). It has no rcfile or profile to dot-source, so its
    /// integration is carried entirely by an injected `PROMPT` — see
    /// `setupCmd`. That buys OSC 2/7/133;A/133;B but not 133;C/D.
    cmd,
    elvish,
    fish,
    nushell,
    /// pwsh / powershell (Windows default shells; T27).
    powershell,
    zsh,
};

/// The result of setting up a shell integration.
pub const ShellIntegration = struct {
    /// The successfully-integrated shell.
    shell: Shell,

    /// The command to use to start the shell with the integration.
    /// In most cases this is identical to the command given but for
    /// bash in particular it may be different.
    ///
    /// The memory is allocated in the arena given to setup.
    command: config.Command,
};

/// Set up the command execution environment for automatic
/// integrated shell integration and return a ShellIntegration
/// struct describing the integration.  If integration fails
/// (shell type couldn't be detected, etc.), this will return null.
///
/// The allocator is used for temporary values and to allocate values
/// in the ShellIntegration result. It is expected to be an arena to
/// simplify cleanup.
pub fn setup(
    alloc_arena: Allocator,
    resource_dir: []const u8,
    command: config.Command,
    env: *EnvMap,
    force_shell: ?Shell,
) !?ShellIntegration {
    const shell: Shell = force_shell orelse
        try detectShell(alloc_arena, command) orelse
        return null;

    const new_command: config.Command = switch (shell) {
        .bash => try setupBash(
            alloc_arena,
            command,
            resource_dir,
            env,
        ),

        .cmd => try setupCmd(
            alloc_arena,
            command,
            env,
        ),

        .nushell => try setupNushell(
            alloc_arena,
            command,
            resource_dir,
            env,
        ),

        .powershell => try setupPowershell(
            alloc_arena,
            command,
            resource_dir,
            env,
        ),

        .zsh => try setupZsh(
            alloc_arena,
            command,
            resource_dir,
            env,
        ),

        .elvish, .fish => xdg: {
            if (!try setupXdgDataDirs(alloc_arena, resource_dir, env)) return null;
            break :xdg try command.clone(alloc_arena);
        },
    } orelse return null;

    return .{
        .shell = shell,
        .command = new_command,
    };
}

test "force shell" {
    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    inline for (@typeInfo(Shell).@"enum".fields) |field| {
        const shell = @field(Shell, field.name);

        var res: TmpResourcesDir = try .init(alloc, shell);
        defer res.deinit();

        const result = try setup(
            alloc,
            res.path,
            .{ .shell = "sh" },
            &env,
            shell,
        );
        try testing.expectEqual(shell, result.?.shell);
    }
}

test "shell integration failure" {
    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const result = try setup(
        alloc,
        "/nonexistent",
        .{ .shell = "sh" },
        &env,
        null,
    );

    try testing.expect(result == null);
    try testing.expectEqual(0, env.count());
}

/// The longest shell name the tables here compare against, and therefore the
/// only buffer size `shellName` ever needs.
const shell_name_max = 16;

/// Reduce an argv0 to the name the shell tables compare against: basename,
/// a trailing ".exe" stripped, lowercased into `buf`.
///
/// On Windows a shell is routinely named with the extension or by full path
/// ("bash.exe", "C:\...\bash.exe"), and Windows filenames are
/// case-insensitive, so both normalizations happen before any name check. A
/// name longer than the buffer cannot match any shell we know, so it comes
/// back null.
fn shellName(buf: []u8, arg0: []const u8) ?[]const u8 {
    const exe = std.fs.path.basename(arg0);
    const stripped = if (std.ascii.endsWithIgnoreCase(exe, ".exe"))
        exe[0 .. exe.len - 4]
    else
        exe;
    if (stripped.len > buf.len) return null;
    return std.ascii.lowerString(buf[0..stripped.len], stripped);
}

fn detectShell(alloc: Allocator, command: config.Command) !?Shell {
    var arg_iter = try command.argIterator(alloc);
    defer arg_iter.deinit();

    const arg0 = arg_iter.next() orelse return null;
    var buf: [shell_name_max]u8 = undefined;
    const name = shellName(&buf, arg0) orelse return null;

    if (std.mem.eql(u8, "bash", name)) {
        // Apple distributes their own patched version of Bash 3.2
        // on macOS that disables the ENV-based POSIX startup path.
        // This means we're unable to perform our automatic shell
        // integration sequence in this specific environment.
        //
        // If we're running "/bin/bash" on Darwin, we can assume
        // we're using Apple's Bash because /bin is non-writable
        // on modern macOS due to System Integrity Protection.
        if (comptime builtin.target.os.tag.isDarwin()) {
            if (std.mem.eql(u8, "/bin/bash", arg0)) {
                return null;
            }
        }
        return .bash;
    }

    if (std.mem.eql(u8, "elvish", name)) return .elvish;
    if (std.mem.eql(u8, "fish", name)) return .fish;
    if (std.mem.eql(u8, "nu", name)) return .nushell;
    if (std.mem.eql(u8, "zsh", name)) return .zsh;

    if (std.mem.eql(u8, "cmd", name)) return .cmd;
    if (std.mem.eql(u8, "pwsh", name)) return .powershell;
    if (std.mem.eql(u8, "powershell", name)) return .powershell;

    return null;
}

test detectShell {
    const testing = std.testing;
    const alloc = testing.allocator;

    try testing.expect(try detectShell(alloc, .{ .shell = "sh" }) == null);
    try testing.expectEqual(.bash, try detectShell(alloc, .{ .shell = "bash" }));
    try testing.expectEqual(.elvish, try detectShell(alloc, .{ .shell = "elvish" }));
    try testing.expectEqual(.fish, try detectShell(alloc, .{ .shell = "fish" }));
    try testing.expectEqual(.nushell, try detectShell(alloc, .{ .shell = "nu" }));
    try testing.expectEqual(.zsh, try detectShell(alloc, .{ .shell = "zsh" }));

    if (comptime builtin.target.os.tag.isDarwin()) {
        try testing.expect(try detectShell(alloc, .{ .shell = "/bin/bash" }) == null);
    }

    try testing.expectEqual(.bash, try detectShell(alloc, .{ .shell = "bash -c 'command'" }));
    try testing.expectEqual(.bash, try detectShell(alloc, .{ .shell = "\"/a b/bash\"" }));

    // Windows spellings: extension, mixed case, full path (T513).
    try testing.expectEqual(.bash, try detectShell(alloc, .{ .shell = "bash.exe" }));
    try testing.expectEqual(.bash, try detectShell(alloc, .{ .shell = "Bash.EXE" }));
    try testing.expectEqual(.nushell, try detectShell(alloc, .{ .shell = "nu.exe" }));
    try testing.expectEqual(.zsh, try detectShell(alloc, .{ .shell = "zsh.exe" }));
    try testing.expectEqual(.fish, try detectShell(alloc, .{ .shell = "fish.exe" }));
    try testing.expectEqual(.powershell, try detectShell(alloc, .{ .shell = "pwsh.exe" }));
    try testing.expectEqual(.powershell, try detectShell(alloc, .{ .shell = "PowerShell.exe" }));
    try testing.expectEqual(.cmd, try detectShell(alloc, .{ .shell = "cmd.exe" }));
    try testing.expectEqual(.cmd, try detectShell(alloc, .{ .shell = "CMD.EXE" }));
    try testing.expectEqual(.cmd, try detectShell(alloc, .{ .shell = "cmd" }));
    if (comptime builtin.target.os.tag == .windows) {
        // Backslash paths only split on Windows' basename rules.
        try testing.expectEqual(.bash, try detectShell(
            alloc,
            .{ .shell = "\"C:\\Program Files\\Git\\bin\\bash.exe\" -l" },
        ));
        try testing.expectEqual(.nushell, try detectShell(
            alloc,
            .{ .direct = &.{"C:\\Users\\x\\AppData\\Local\\Programs\\nu\\bin\\nu.exe"} },
        ));
    }
}

/// Programs that ARE shells for the purpose of `bareShellCommand` but that
/// `detectShell` returns null for, because there is no integration to inject.
///
/// The bar for a row here is that the pane's backend already knows how to run
/// the program as an interactive shell — the agent's `windowsCommandArgs`
/// table (`src/remote/agent/pty_child.zig`) is the other half of the pair, and
/// `wsl` has a row there. It is deliberately an allowlist and not "any single
/// bare word": `command = htop` is a COMMAND, and reinterpreting it as a shell
/// would silently change what happens when it exits. Nothing checks that the
/// two tables agree — T1667.
const bare_shells_without_integration = [_][]const u8{"wsl"};

/// Answer whether `command` is nothing but a BARE shell — a single argv0
/// with no arguments naming a shell we know. Returns the argv0 duped into
/// `alloc` (caller frees), or null when the command carries arguments or
/// names anything else.
///
/// "A shell we know" is deliberately BROADER than `detectShell`, which
/// answers the narrower question "which integration script?" (T864). For
/// every shell in that table the two answers coincide; for `wsl` they do
/// not, and the right answer to *this* function's question is still yes.
///
/// Why it exists (T514): a config `command = pwsh` is a shell CHOICE, but on
/// an agent-backed pane it used to travel as `OPEN.command`, so the agent ran
/// `cmd /c pwsh` — the user's shell nested under the default shell, with the
/// integration argv rewrite dropped (an explicit command suppresses it by
/// design). The caller forwards a bare shell as `OPEN.shell` instead, which
/// unwraps the nesting and re-enables integration. A command WITH arguments
/// keeps command semantics untouched: only the pure "this is my shell"
/// spelling is reinterpreted.
pub fn bareShellCommand(alloc: Allocator, command: config.Command) !?[]const u8 {
    var arg_iter = try command.argIterator(alloc);
    defer arg_iter.deinit();
    const arg0 = arg_iter.next() orelse return null;
    if (arg_iter.next() != null) return null;

    recognized: {
        // The usual case: a shell we have an integration script for.
        if (try detectShell(alloc, command) != null) break :recognized;

        // T864: and the ones we do not. `detectShell` answers "which
        // integration script?", which is a NARROWER question than the one
        // this function asks — "did the user name their shell?" — and for
        // `command = wsl` the answers differ: no integration, but plainly a
        // shell choice. Falling through to OPEN.command ran it as `cmd /c
        // wsl`, so the pane carried a hidden wrapper process, `+list` named
        // the wrapper as the shell, and cwd tracking followed the wrapper
        // instead of the shell.
        var buf: [shell_name_max]u8 = undefined;
        const name = shellName(&buf, arg0) orelse return null;
        for (bare_shells_without_integration) |s| {
            if (std.mem.eql(u8, s, name)) break :recognized;
        }
        return null;
    }

    return try alloc.dupe(u8, arg0);
}

test bareShellCommand {
    const testing = std.testing;
    const alloc = testing.allocator;

    // A bare recognized shell comes back as its argv0.
    {
        const got = (try bareShellCommand(alloc, .{ .shell = "pwsh" })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings("pwsh", got);
    }
    {
        const got = (try bareShellCommand(alloc, .{ .shell = "bash.exe" })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings("bash.exe", got);
    }
    {
        const got = (try bareShellCommand(alloc, .{ .direct = &.{"nu"} })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings("nu", got);
    }

    // Arguments mean a real command line, never a shell choice.
    try testing.expect(try bareShellCommand(alloc, .{ .shell = "pwsh -NoProfile" }) == null);
    try testing.expect(try bareShellCommand(
        alloc,
        .{ .direct = &.{ "bash", "-c", "ls" } },
    ) == null);

    // cmd is a shell like any other since T512, so a bare one unwraps rather
    // than travelling as a command the agent would run under `cmd /c`.
    {
        const got = (try bareShellCommand(alloc, .{ .shell = "cmd.exe" })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings("cmd.exe", got);
    }

    // T864: a shell with no integration is still a shell CHOICE. `wsl` has
    // no integration script (detectShell says null) but the agent knows how
    // to run it, so a bare one must unwrap rather than travel as a command
    // the agent would run under `cmd /c`.
    try testing.expect(try detectShell(alloc, .{ .shell = "wsl" }) == null);
    for ([_][:0]const u8{ "wsl", "wsl.exe", "WSL.EXE" }) |spelling| {
        const got = (try bareShellCommand(alloc, .{ .shell = spelling })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings(spelling, got);
    }
    {
        const got = (try bareShellCommand(alloc, .{ .direct = &.{"wsl"} })).?;
        defer alloc.free(got);
        try testing.expectEqualStrings("wsl", got);
    }
    // ...and it is still only the BARE spelling: `wsl -d Ubuntu` is a command
    // line, with command semantics.
    try testing.expect(try bareShellCommand(
        alloc,
        .{ .direct = &.{ "wsl", "-d", "Ubuntu" } },
    ) == null);

    // Unrecognized programs (scripts are commands).
    try testing.expect(try bareShellCommand(alloc, .{ .shell = "python" }) == null);
    // The allowlist is an allowlist, not "any single bare word" — a command
    // that happens to be one token keeps command semantics.
    try testing.expect(try bareShellCommand(alloc, .{ .shell = "htop" }) == null);

    if (comptime builtin.target.os.tag == .windows) {
        // A quoted full path is still bare: one argv0, recognized.
        {
            const got = (try bareShellCommand(
                alloc,
                .{ .shell = "\"C:\\Program Files\\PowerShell\\7\\pwsh.exe\"" },
            )).?;
            defer alloc.free(got);
            try testing.expectEqualStrings("C:\\Program Files\\PowerShell\\7\\pwsh.exe", got);
        }
        // The allowlist matches on the BASENAME too, which is the spelling
        // section G of `test\win32\gui-launch-command.ps1` launches with —
        // it names its stub by full path, because a stub on %PATH% cannot
        // shadow System32\wsl.exe (CreateProcessW searches System32 first).
        {
            const got = (try bareShellCommand(
                alloc,
                .{ .direct = &.{"C:\\Windows\\System32\\wsl.exe"} },
            )).?;
            defer alloc.free(got);
            try testing.expectEqualStrings("C:\\Windows\\System32\\wsl.exe", got);
        }
    }
}

/// Set up the shell integration features environment variable.
pub fn setupFeatures(
    env: *EnvMap,
    features: config.ShellIntegrationFeatures,
    cursor_blink: bool,
) !void {
    const fields = @typeInfo(@TypeOf(features)).@"struct".fields;
    const capacity: usize = capacity: {
        comptime var n: usize = fields.len - 1; // commas
        inline for (fields) |field| n += field.name.len;
        n += ":steady".len; // cursor value
        break :capacity n;
    };

    var buf: [capacity]u8 = undefined;
    var writer: std.Io.Writer = .fixed(&buf);

    // Sort the fields so that the output is deterministic. This is
    // done at comptime so it has no runtime cost
    const fields_sorted: [fields.len][]const u8 = comptime fields: {
        var fields_sorted: [fields.len][]const u8 = undefined;
        for (fields, 0..) |field, i| fields_sorted[i] = field.name;
        std.mem.sortUnstable(
            []const u8,
            &fields_sorted,
            {},
            (struct {
                fn lessThan(_: void, lhs: []const u8, rhs: []const u8) bool {
                    return std.ascii.orderIgnoreCase(lhs, rhs) == .lt;
                }
            }).lessThan,
        );
        break :fields fields_sorted;
    };

    inline for (fields_sorted) |name| {
        if (@field(features, name)) {
            if (writer.end > 0) try writer.writeByte(',');
            try writer.writeAll(name);

            if (std.mem.eql(u8, name, "cursor")) {
                try writer.writeAll(if (cursor_blink) ":blink" else ":steady");
            }
        }
    }

    if (writer.end > 0) {
        try env.put("GHOSTTY_SHELL_FEATURES", buf[0..writer.end]);
    }
}

test "setup features" {
    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    // Test: all features enabled
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try setupFeatures(&env, .{ .cursor = true, .sudo = true, .title = true, .@"ssh-env" = true, .@"ssh-terminfo" = true, .path = true }, true);
        try testing.expectEqualStrings("cursor:blink,path,ssh-env,ssh-terminfo,sudo,title", env.get("GHOSTTY_SHELL_FEATURES").?);
    }

    // Test: all features disabled
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try setupFeatures(&env, std.mem.zeroes(config.ShellIntegrationFeatures), true);
        try testing.expect(env.get("GHOSTTY_SHELL_FEATURES") == null);
    }

    // Test: mixed features
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try setupFeatures(&env, .{ .cursor = false, .sudo = true, .title = false, .@"ssh-env" = true, .@"ssh-terminfo" = false, .path = false }, true);
        try testing.expectEqualStrings("ssh-env,sudo", env.get("GHOSTTY_SHELL_FEATURES").?);
    }

    // Test: blinking cursor
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();
        try setupFeatures(&env, .{ .cursor = true, .sudo = false, .title = false, .@"ssh-env" = false, .@"ssh-terminfo" = false, .path = false }, true);
        try testing.expectEqualStrings("cursor:blink", env.get("GHOSTTY_SHELL_FEATURES").?);
    }

    // Test: steady cursor
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();
        try setupFeatures(&env, .{ .cursor = true, .sudo = false, .title = false, .@"ssh-env" = false, .@"ssh-terminfo" = false, .path = false }, false);
        try testing.expectEqualStrings("cursor:steady", env.get("GHOSTTY_SHELL_FEATURES").?);
    }
}

/// Accumulates the rewritten invocation for the setups that BUILD a new
/// command (bash, nushell) rather than cloning the one they were given, and
/// hands it back under the SAME tag it came in as (T862).
///
/// The tag matters because it decides whether the words can be recovered. A
/// `.shell` command is space-joined with no quoting rules, so the moment one
/// argument contains a space the round trip through `argIterator` produces
/// different words than went in — which is how `C:\Program Files\Git\bin\
/// bash.exe` reached the agent as `OPEN.argv[0] = "C:\Program"`. A `.direct`
/// command carries its argv as an array and cannot lose a boundary, so a
/// caller that handed us one (every caller naming a PATH does) gets one back.
///
/// A `.shell` input keeps the old space-joined string exactly as before, so
/// nothing about the plain `command = bash --norc` config changes.
const RewriteBuilder = struct {
    tag: std.meta.Tag(config.Command),
    args: std.ArrayList([:0]const u8),
    text: internal_os.shell.ShellCommandBuilder,

    fn init(alloc: Allocator, command: config.Command) RewriteBuilder {
        return .{
            .tag = std.meta.activeTag(command),
            .args = .empty,
            .text = .init(alloc),
        };
    }

    fn deinit(self: *RewriteBuilder, alloc: Allocator) void {
        self.args.deinit(alloc);
        self.text.deinit();
    }

    /// Append one argument. Empty arguments are dropped, matching
    /// `ShellCommandBuilder.appendArg` (a `.shell` command cannot express one).
    fn appendArg(self: *RewriteBuilder, alloc: Allocator, arg: []const u8) !void {
        if (arg.len == 0) return;
        switch (self.tag) {
            .shell => try self.text.appendArg(arg),
            .direct => try self.args.append(alloc, try alloc.dupeZ(u8, arg)),
        }
    }

    /// Append a flag whose value needs shell quoting to survive a `.shell`
    /// command's space-join (nushell's `--execute 'use ghostty *'`).
    ///
    /// `.shell` takes the caller's pre-quoted spelling verbatim — the quotes
    /// are what the shell-words parse on the way back out reads. `.direct` has
    /// no such parse, so it takes the flag and the RAW value as two argv
    /// elements; pushing the quoted spelling in there would hand nushell a
    /// single argument with literal quote characters in it.
    fn appendQuotedPair(
        self: *RewriteBuilder,
        alloc: Allocator,
        shell_form: []const u8,
        flag: []const u8,
        value: []const u8,
    ) !void {
        switch (self.tag) {
            .shell => try self.text.appendArg(shell_form),
            .direct => {
                try self.args.append(alloc, try alloc.dupeZ(u8, flag));
                try self.args.append(alloc, try alloc.dupeZ(u8, value));
            },
        }
    }

    fn finish(self: *RewriteBuilder, alloc: Allocator) !config.Command {
        return switch (self.tag) {
            .shell => .{ .shell = try alloc.dupeZ(u8, try self.text.toOwnedSlice()) },
            .direct => .{ .direct = try self.args.toOwnedSlice(alloc) },
        };
    }
};

/// Setup the bash automatic shell integration. This works by
/// starting bash in POSIX mode and using the ENV environment
/// variable to load our bash integration script. This prevents
/// bash from loading its normal startup files, which becomes
/// our script's responsibility (along with disabling POSIX
/// mode).
///
/// This returns a new (allocated) shell command string that
/// enables the integration or null if integration failed.
fn setupBash(
    alloc: Allocator,
    command: config.Command,
    resource_dir: []const u8,
    env: *EnvMap,
) !?config.Command {
    var cmd: RewriteBuilder = .init(alloc, command);
    defer cmd.deinit(alloc);

    // Iterator that yields each argument in the original command line.
    // This will allocate once proportionate to the command line length.
    var iter = try command.argIterator(alloc);
    defer iter.deinit();

    // Start accumulating arguments with the executable and initial flags.
    if (iter.next()) |exe| {
        try cmd.appendArg(alloc, exe);
    } else return null;
    try cmd.appendArg(alloc, "--posix");

    // Stores the list of intercepted command line flags that will be passed
    // to our shell integration script: --norc --noprofile
    // We always include at least "1" so the script can differentiate between
    // being manually sourced or automatically injected (from here).
    var buf: [32]u8 = undefined;
    var inject: std.Io.Writer = .fixed(&buf);
    try inject.writeAll("1");

    // Walk through the rest of the given arguments. If we see an option that
    // would require complex or unsupported integration behavior, we bail out
    // and skip loading our shell integration. Users can still manually source
    // the shell integration script.
    //
    // Unsupported options:
    //  -c          -c is always non-interactive
    //  --posix     POSIX mode (a la /bin/sh)
    var rcfile: ?[]const u8 = null;
    while (iter.next()) |arg| {
        if (std.mem.eql(u8, arg, "--posix")) {
            return null;
        } else if (std.mem.eql(u8, arg, "--norc")) {
            try inject.writeAll(" --norc");
        } else if (std.mem.eql(u8, arg, "--noprofile")) {
            try inject.writeAll(" --noprofile");
        } else if (std.mem.eql(u8, arg, "--rcfile") or std.mem.eql(u8, arg, "--init-file")) {
            rcfile = iter.next();
        } else if (arg.len > 1 and arg[0] == '-' and arg[1] != '-') {
            // '-c command' is always non-interactive
            if (std.mem.indexOfScalar(u8, arg, 'c') != null) {
                return null;
            }
            try cmd.appendArg(alloc, arg);
        } else if (std.mem.eql(u8, arg, "-") or std.mem.eql(u8, arg, "--")) {
            // All remaining arguments should be passed directly to the shell
            // command. We shouldn't perform any further option processing.
            try cmd.appendArg(alloc, arg);
            while (iter.next()) |remaining_arg| {
                try cmd.appendArg(alloc, remaining_arg);
            }
            break;
        } else {
            try cmd.appendArg(alloc, arg);
        }
    }

    // Preserve an existing ENV value. We're about to overwrite it.
    if (env.get("ENV")) |v| {
        try env.put("GHOSTTY_BASH_ENV", v);
    }

    // Set our new ENV to point to our integration script.
    var script_path_buf: [std.fs.max_path_bytes]u8 = undefined;
    const script_path = try std.fmt.bufPrint(
        &script_path_buf,
        "{s}/shell-integration/bash/ghostty.bash",
        .{resource_dir},
    );
    if (std.fs.openFileAbsolute(script_path, .{})) |file| {
        file.close();
        try env.put("ENV", script_path);
    } else |err| {
        log.warn("unable to open {s}: {}", .{ script_path, err });
        env.remove("GHOSTTY_BASH_ENV");
        return null;
    }

    try env.put("GHOSTTY_BASH_INJECT", buf[0..inject.end]);
    if (rcfile) |v| {
        try env.put("GHOSTTY_BASH_RCFILE", v);
    }

    // In POSIX mode, HISTFILE defaults to ~/.sh_history, so unless we're
    // staying in POSIX mode (--posix), change it back to ~/.bash_history.
    if (env.get("HISTFILE") == null) {
        var home_buf: [1024]u8 = undefined;
        if (try homedir.home(&home_buf)) |home| {
            var histfile_buf: [std.fs.max_path_bytes]u8 = undefined;
            const histfile = try std.fmt.bufPrint(
                &histfile_buf,
                "{s}/.bash_history",
                .{home},
            );
            try env.put("HISTFILE", histfile);
            try env.put("GHOSTTY_BASH_UNEXPORT_HISTFILE", "1");
        }
    }

    // Return a copy of our modified command line to use as the shell command.
    return try cmd.finish(alloc);
}

test "bash" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const command = try setupBash(alloc, .{ .shell = "bash" }, res.path, &env);
    try testing.expectEqualStrings("bash --posix", command.?.shell);
    try testing.expectEqualStrings("1", env.get("GHOSTTY_BASH_INJECT").?);

    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/ghostty.bash", .{res.shell_path}),
        env.get("ENV").?,
    );
}

test "bash: a direct command keeps its argv boundaries (T862)" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    // The shape `Surface.zig` sends for `--shell=C:\Program Files\...`: ONE
    // argv element that happens to contain a space. The rewrite must hand
    // back an argv, not a space-joined string that re-splits into
    // `C:\Program` + `Files\Git\bin\bash.exe`.
    const spaced = "C:\\Program Files\\Git\\bin\\bash.exe";
    const argv: []const [:0]const u8 = &.{spaced};
    const command = try setupBash(alloc, .{ .direct = argv }, res.path, &env);
    try testing.expect(command.? == .direct);
    try testing.expectEqual(@as(usize, 2), command.?.direct.len);
    try testing.expectEqualStrings(spaced, command.?.direct[0]);
    try testing.expectEqualStrings("--posix", command.?.direct[1]);

    // And the round trip a caller actually makes is lossless.
    var it = try command.?.argIterator(alloc);
    defer it.deinit();
    try testing.expectEqualStrings(spaced, it.next().?);
    try testing.expectEqualStrings("--posix", it.next().?);
    try testing.expect(it.next() == null);
}

test "bash: a direct command carries its own flags through (T862)" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const argv: []const [:0]const u8 = &.{ "/a b/bash", "--norc", "-l" };
    const command = try setupBash(alloc, .{ .direct = argv }, res.path, &env);
    try testing.expect(command.? == .direct);
    try testing.expectEqual(@as(usize, 3), command.?.direct.len);
    try testing.expectEqualStrings("/a b/bash", command.?.direct[0]);
    try testing.expectEqualStrings("--posix", command.?.direct[1]);
    try testing.expectEqualStrings("-l", command.?.direct[2]);
    // --norc is intercepted into the inject list, exactly as for `.shell`.
    try testing.expectEqualStrings("1 --norc", env.get("GHOSTTY_BASH_INJECT").?);
}

test "detectShell: a direct spaced path is one argument (T862)" {
    const testing = std.testing;
    const alloc = testing.allocator;

    // The `.shell` spelling is what broke: shell-words splits at the space
    // and `detectShell` basenames `Program`.
    try testing.expect(try detectShell(
        alloc,
        .{ .shell = "C:\\Program Files\\Git\\bin\\bash.exe" },
    ) == null);

    // The `.direct` spelling the shell-path callers now use detects.
    const argv: []const [:0]const u8 = &.{"C:\\Program Files\\Git\\bin\\bash.exe"};
    try testing.expectEqual(.bash, try detectShell(alloc, .{ .direct = argv }));

    const pwsh: []const [:0]const u8 = &.{"C:\\Program Files\\PowerShell\\7\\pwsh.exe"};
    try testing.expectEqual(.powershell, try detectShell(alloc, .{ .direct = pwsh }));

    const nu: []const [:0]const u8 = &.{"C:\\Program Files\\nu\\bin\\nu.exe"};
    try testing.expectEqual(.nushell, try detectShell(alloc, .{ .direct = nu }));
}

test "bash: unsupported options" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    const cmdlines = [_][:0]const u8{
        "bash --posix",
        "bash --rcfile script.sh --posix",
        "bash --init-file script.sh --posix",
        "bash -c script.sh",
        "bash -ic script.sh",
    };

    for (cmdlines) |cmdline| {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try testing.expect(try setupBash(alloc, .{ .shell = cmdline }, res.path, &env) == null);
        try testing.expectEqual(0, env.count());
    }
}

test "bash: inject flags" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    // bash --norc
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        const command = try setupBash(alloc, .{ .shell = "bash --norc" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix", command.?.shell);
        try testing.expectEqualStrings("1 --norc", env.get("GHOSTTY_BASH_INJECT").?);
    }

    // bash --noprofile
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        const command = try setupBash(alloc, .{ .shell = "bash --noprofile" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix", command.?.shell);
        try testing.expectEqualStrings("1 --noprofile", env.get("GHOSTTY_BASH_INJECT").?);
    }
}

test "bash: rcfile" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    // bash --rcfile
    {
        const command = try setupBash(alloc, .{ .shell = "bash --rcfile profile.sh" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix", command.?.shell);
        try testing.expectEqualStrings("profile.sh", env.get("GHOSTTY_BASH_RCFILE").?);
    }

    // bash --init-file
    {
        const command = try setupBash(alloc, .{ .shell = "bash --init-file profile.sh" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix", command.?.shell);
        try testing.expectEqualStrings("profile.sh", env.get("GHOSTTY_BASH_RCFILE").?);
    }
}

test "bash: HISTFILE" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    // HISTFILE unset
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        _ = try setupBash(alloc, .{ .shell = "bash" }, res.path, &env);
        try testing.expect(std.mem.endsWith(u8, env.get("HISTFILE").?, ".bash_history"));
        try testing.expectEqualStrings("1", env.get("GHOSTTY_BASH_UNEXPORT_HISTFILE").?);
    }

    // HISTFILE set
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try env.put("HISTFILE", "my_history");

        _ = try setupBash(alloc, .{ .shell = "bash" }, res.path, &env);
        try testing.expectEqualStrings("my_history", env.get("HISTFILE").?);
        try testing.expect(env.get("GHOSTTY_BASH_UNEXPORT_HISTFILE") == null);
    }
}

test "bash: ENV" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try env.put("ENV", "env.sh");

    _ = try setupBash(alloc, .{ .shell = "bash" }, res.path, &env);
    try testing.expectEqualStrings("env.sh", env.get("GHOSTTY_BASH_ENV").?);

    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/ghostty.bash", .{res.shell_path}),
        env.get("ENV").?,
    );
}

test "bash: additional arguments" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .bash);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    // "-" argument separator
    {
        const command = try setupBash(alloc, .{ .shell = "bash - --arg file1 file2" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix - --arg file1 file2", command.?.shell);
    }

    // "--" argument separator
    {
        const command = try setupBash(alloc, .{ .shell = "bash -- --arg file1 file2" }, res.path, &env);
        try testing.expectEqualStrings("bash --posix -- --arg file1 file2", command.?.shell);
    }
}

test "bash: missing resources" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();

    const resources_dir = try tmp_dir.dir.realpathAlloc(alloc, ".");
    defer alloc.free(resources_dir);

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try testing.expect(try setupBash(alloc, .{ .shell = "bash" }, resources_dir, &env) == null);
    try testing.expectEqual(0, env.count());
}

/// Setup automatic shell integration for shells that include
/// their modules from paths in `XDG_DATA_DIRS` env variable.
///
/// The shell-integration path is prepended to `XDG_DATA_DIRS`.
/// It is also saved in the `GHOSTTY_SHELL_INTEGRATION_XDG_DIR` variable
/// so that the shell can refer to it and safely remove this directory
/// from `XDG_DATA_DIRS` when integration is complete.
fn setupXdgDataDirs(
    alloc: Allocator,
    resource_dir: []const u8,
    env: *EnvMap,
) !bool {
    var path_buf: [std.fs.max_path_bytes]u8 = undefined;

    // Get our path to the shell integration directory.
    const integ_path = try std.fmt.bufPrint(
        &path_buf,
        "{s}/shell-integration",
        .{resource_dir},
    );
    var integ_dir = std.fs.openDirAbsolute(integ_path, .{}) catch |err| {
        log.warn("unable to open {s}: {}", .{ integ_path, err });
        return false;
    };
    integ_dir.close();

    // Set an env var so we can remove this from XDG_DATA_DIRS later.
    // This happens in the shell integration config itself. We do this
    // so that our modifications don't interfere with other commands.
    try env.put("GHOSTTY_SHELL_INTEGRATION_XDG_DIR", integ_path);

    // We attempt to avoid allocating by using the stack up to 4K.
    // Max stack size is considerably larger on mac
    // 4K is a reasonable size for this for most cases. However, env
    // vars can be significantly larger so if we have to we fall
    // back to a heap allocated value.
    var stack_alloc_state = std.heap.stackFallback(4096, alloc);
    const stack_alloc = stack_alloc_state.get();

    // If no XDG_DATA_DIRS set use the default value as specified.
    // This ensures that the default directories aren't lost by setting
    // our desired integration dir directly. See #2711.
    // <https://specifications.freedesktop.org/basedir-spec/0.6/#variables>
    const xdg_data_dirs_key = "XDG_DATA_DIRS";
    try env.put(
        xdg_data_dirs_key,
        try internal_os.prependEnv(
            stack_alloc,
            env.get(xdg_data_dirs_key) orelse "/usr/local/share:/usr/share",
            integ_path,
        ),
    );

    return true;
}

test "xdg: empty XDG_DATA_DIRS" {
    if (builtin.os.tag == .windows) return error.SkipZigTest;

    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .fish);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try testing.expect(try setupXdgDataDirs(alloc, res.path, &env));

    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration", .{res.path}),
        env.get("GHOSTTY_SHELL_INTEGRATION_XDG_DIR").?,
    );
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration:/usr/local/share:/usr/share", .{res.path}),
        env.get("XDG_DATA_DIRS").?,
    );
}

test "xdg: existing XDG_DATA_DIRS" {
    if (builtin.os.tag == .windows) return error.SkipZigTest;

    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .fish);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try env.put("XDG_DATA_DIRS", "/opt/share");

    try testing.expect(try setupXdgDataDirs(alloc, res.path, &env));

    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration", .{res.path}),
        env.get("GHOSTTY_SHELL_INTEGRATION_XDG_DIR").?,
    );
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration:/opt/share", .{res.path}),
        env.get("XDG_DATA_DIRS").?,
    );
}

test "xdg: missing resources" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();

    const resources_dir = try tmp_dir.dir.realpathAlloc(alloc, ".");
    defer alloc.free(resources_dir);

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try testing.expect(!try setupXdgDataDirs(alloc, resources_dir, &env));
    try testing.expectEqual(0, env.count());
}

/// Set up automatic Nushell shell integration. This works by adding our
/// shell resource directory to the `XDG_DATA_DIRS` environment variable,
/// which Nushell will use to load `nushell/vendor/autoload/ghostty.nu`.
///
/// We then add `--execute 'use ghostty ...'` to the nu command line to
/// automatically enable our shelll features.
fn setupNushell(
    alloc: Allocator,
    command: config.Command,
    resource_dir: []const u8,
    env: *EnvMap,
) !?config.Command {
    // Add our XDG_DATA_DIRS entry (for nushell/vendor/autoload/). This
    // makes our 'ghostty' module automatically available, even if any
    // of the later checks abort the rest of our automatic integration.
    if (!try setupXdgDataDirs(alloc, resource_dir, env)) return null;

    var cmd: RewriteBuilder = .init(alloc, command);
    defer cmd.deinit(alloc);

    // Iterator that yields each argument in the original command line.
    // This will allocate once proportionate to the command line length.
    var iter = try command.argIterator(alloc);
    defer iter.deinit();

    // Start accumulating arguments with the executable and initial flags.
    if (iter.next()) |exe| {
        try cmd.appendArg(alloc, exe);
    } else return null;

    // Tell nu to immediately "use" all of the exported functions in our
    // 'ghostty' module.
    //
    // We can consider making this more specific based on the set of
    // enabled shell features (e.g. `use ghostty sudo`). At the moment,
    // shell features are all runtime-guarded in the nushell script.
    try cmd.appendQuotedPair(
        alloc,
        "--execute 'use ghostty *'",
        "--execute",
        "use ghostty *",
    );

    // Walk through the rest of the given arguments. If we see an option that
    // would require complex or unsupported integration behavior, we bail out
    // and skip loading our shell integration. Users can still manually source
    // the shell integration module.
    //
    // Unsupported options:
    //  -c / --command      -c is always non-interactive
    //  --lsp               --lsp starts the language server
    while (iter.next()) |arg| {
        if (std.mem.eql(u8, arg, "--command") or std.mem.eql(u8, arg, "--lsp")) {
            return null;
        } else if (arg.len > 1 and arg[0] == '-' and arg[1] != '-') {
            if (std.mem.indexOfScalar(u8, arg, 'c') != null) {
                return null;
            }
            try cmd.appendArg(alloc, arg);
        } else if (std.mem.eql(u8, arg, "-") or std.mem.eql(u8, arg, "--")) {
            // All remaining arguments should be passed directly to the shell
            // command. We shouldn't perform any further option processing.
            try cmd.appendArg(alloc, arg);
            while (iter.next()) |remaining_arg| {
                try cmd.appendArg(alloc, remaining_arg);
            }
            break;
        } else {
            try cmd.appendArg(alloc, arg);
        }
    }

    // Return a copy of our modified command line to use as the shell command.
    return try cmd.finish(alloc);
}

/// The OSC 133;A prompt mark, written in cmd's PROMPT escape language
/// (`$E` is ESC, and `$E\` is therefore ST). Also doubles as the marker that
/// says our integration is already in a PROMPT we inherited.
const cmd_prompt_mark = "$E]133;A$E\\";

/// Set up cmd.exe integration (T512).
///
/// cmd has no profile, no rcfile and no prompt hook, so for a long time it was
/// treated as unintegrable. What it does have is `PROMPT`, whose `$E` expands
/// to ESC and which is re-rendered on every prompt — enough to emit OSC
/// sequences at prompt time. Windows Terminal uses exactly this mechanism for
/// its own cwd tracking.
///
/// What that buys, versus a real integration:
///
///   * OSC 7   — working-directory reporting, which stands the T185 live
///               process-cwd fallback down AND, by way of `reportPwd`'s
///               untitled-window rule, makes the tab title follow the
///               directory: a strip of cmd tabs stops being a row of
///               identical labels
///   * OSC 133;A / 133;B — prompt start and prompt end, so prompt jumping
///               works. There is no hook for 133;C/D (command start and exit
///               status), and cmd offers no way to get one.
///
/// Deliberately NOT OSC 2, which every other integration here sends. cmd's
/// `title` command is the documented way a user names a window, and an OSC 2
/// on every prompt would overwrite it a fraction of a second later — the
/// terminal already does the right thing without one, showing the working
/// directory until something sets a real title and then leaving that alone
/// (`stream_handler.zig`'s `seen_title`).
///
/// The user's own `PROMPT` is WRAPPED, never replaced, so a customized prompt
/// still renders. `argv` is left completely alone — unlike every other shell
/// here, this integration is carried by the environment only.
fn setupCmd(
    alloc: Allocator,
    command: config.Command,
    env: *EnvMap,
) !?config.Command {
    // `cmd /c ...` and `cmd /k ...` are running a command rather than opening
    // an interactive shell. Injecting a prompt into those would emit OSC into
    // the middle of somebody's script output, so leave them alone entirely.
    {
        var iter = try command.argIterator(alloc);
        defer iter.deinit();
        _ = iter.next() orelse return null;
        while (iter.next()) |arg| {
            if (arg.len < 2) continue;
            if (arg[0] != '/' and arg[0] != '-') continue;
            switch (std.ascii.toLower(arg[1])) {
                'c', 'k' => return null,
                else => {},
            }
        }
    }

    // cmd's own default when PROMPT is unset is `$P$G` ("C:\dir>").
    const user_prompt = env.get("PROMPT") orelse "$P$G";

    // A nested cmd inherits the PROMPT we already wrote. Wrapping it a second
    // time would emit every sequence twice per prompt, so this is a no-op when
    // our mark is already there.
    if (std.mem.indexOf(u8, user_prompt, cmd_prompt_mark) != null) {
        return try command.clone(alloc);
    }

    // The OSC 7 host is `localhost` rather than the machine name: PROMPT does
    // not expand `%COMPUTERNAME%` at render time, and OSC 7's locality check
    // accepts `localhost` unconditionally. The `kitty-shell-cwd` scheme takes
    // the path raw, which is what lets `$P`'s native `D:\dir` spelling through
    // unescaped — stream_handler's reportPwd normalizes it back.
    const prompt = try std.fmt.allocPrint(alloc, "{s}$E]7;kitty-shell-cwd://localhost/$P$E\\{s}$E]133;B$E\\", .{
        cmd_prompt_mark,
        user_prompt,
    });
    try env.put("PROMPT", prompt);

    return try command.clone(alloc);
}

/// Set up PowerShell (pwsh 7 / Windows PowerShell 5.1) integration (T27).
///
/// PowerShell has no ENV/rcfile hook we can inject from the outside, so the
/// integration is dot-sourced with `-NoExit -Command . '<script>'` before
/// the interactive session starts. `.direct` argv form is required on
/// Windows (the `.shell` string form whitespace-splits with no quoting).
///
/// Bails out (no integration, unmodified command) when the user's command
/// already carries `-Command`/`-c` or `-File`/`-f`: those are
/// non-interactive and injecting would change their semantics.
fn setupPowershell(
    alloc: Allocator,
    command: config.Command,
    resource_dir: []const u8,
    env: *EnvMap,
) !?config.Command {
    const script = try std.fs.path.join(alloc, &.{
        resource_dir,
        "shell-integration",
        "powershell",
        "ghostty.ps1",
    });

    // The script must exist; otherwise the shell would error on startup.
    std.fs.accessAbsolute(script, .{}) catch return null;

    var args: std.ArrayList([:0]const u8) = .empty;

    var iter = try command.argIterator(alloc);
    defer iter.deinit();

    const exe = iter.next() orelse return null;
    try args.append(alloc, try alloc.dupeZ(u8, exe));

    while (iter.next()) |arg| {
        // Non-interactive invocations: leave them alone entirely.
        if (std.ascii.eqlIgnoreCase(arg, "-Command") or
            std.ascii.eqlIgnoreCase(arg, "-c") or
            std.ascii.eqlIgnoreCase(arg, "-File") or
            std.ascii.eqlIgnoreCase(arg, "-f") or
            std.ascii.eqlIgnoreCase(arg, "-EncodedCommand"))
        {
            return null;
        }
        try args.append(alloc, try alloc.dupeZ(u8, arg));
    }

    // Dot-source the integration, then drop into the interactive shell.
    try args.append(alloc, "-NoExit");
    try args.append(alloc, "-Command");
    try args.append(alloc, try std.fmt.allocPrintSentinel(
        alloc,
        ". '{s}'",
        .{script},
        0,
    ));

    // The script reads this to know where the resources live (parity with
    // the other integrations).
    try env.put("GHOSTTY_POWERSHELL", script);

    return .{ .direct = try args.toOwnedSlice(alloc) };
}

test "cmd wraps the user's prompt" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var env = EnvMap.init(alloc);
    defer env.deinit();
    try env.put("PROMPT", "[mine]$P$G");
    try env.put("GHOSTTY_SHELL_FEATURES", "cursor:blink,title");

    const command = (try setupCmd(alloc, .{ .shell = "cmd.exe" }, &env)).?;

    // argv is untouched: this integration lives entirely in the environment.
    try testing.expectEqualStrings("cmd.exe", command.shell);

    const prompt = env.get("PROMPT").?;
    try testing.expect(std.mem.startsWith(u8, prompt, "$E]133;A$E\\"));
    try testing.expect(std.mem.endsWith(u8, prompt, "$E]133;B$E\\"));
    // The user's own prompt survives, unmodified and in one piece.
    try testing.expect(std.mem.indexOf(u8, prompt, "[mine]$P$G") != null);
    try testing.expect(std.mem.indexOf(u8, prompt, "$E]7;kitty-shell-cwd://localhost/$P$E\\") != null);

    // No OSC 2, even with the title feature on: cmd's own `title` command has
    // to survive, and the terminal titles an untitled window from the pwd for
    // us. An OSC 2 here would overwrite `title foo` at the very next prompt.
    try testing.expect(std.mem.indexOf(u8, prompt, "]2;") == null);
}

test "cmd defaults, gating and idempotence" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    // No PROMPT set: cmd's own default ($P$G) is what gets wrapped.
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();
        _ = (try setupCmd(alloc, .{ .shell = "cmd" }, &env)).?;
        const prompt = env.get("PROMPT").?;
        try testing.expect(std.mem.indexOf(u8, prompt, "$P$G$E]133;B") != null);
    }

    // Running it twice does not stack a second copy (a nested cmd inherits
    // the PROMPT we wrote).
    {
        var env = EnvMap.init(alloc);
        defer env.deinit();
        _ = (try setupCmd(alloc, .{ .shell = "cmd" }, &env)).?;
        const once = try alloc.dupe(u8, env.get("PROMPT").?);
        _ = (try setupCmd(alloc, .{ .shell = "cmd" }, &env)).?;
        try testing.expectEqualStrings(once, env.get("PROMPT").?);
    }

    // /c and /k are commands, not interactive shells: no integration at all.
    for ([_][:0]const u8{ "cmd /c dir", "cmd.exe /K build.bat", "cmd -c dir" }) |line| {
        var env = EnvMap.init(alloc);
        defer env.deinit();
        try testing.expect(try setupCmd(alloc, .{ .shell = line }, &env) == null);
        try testing.expect(env.get("PROMPT") == null);
    }
}

test "powershell" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .powershell);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const result = (try setup(
        alloc,
        res.path,
        .{ .shell = "pwsh" },
        &env,
        null,
    )).?;
    try testing.expectEqual(.powershell, result.shell);

    // Dot-sources the integration and keeps the session interactive.
    const argv = result.command.direct;
    try testing.expectEqualStrings("pwsh", argv[0]);
    try testing.expectEqualStrings("-NoExit", argv[argv.len - 3]);
    try testing.expectEqualStrings("-Command", argv[argv.len - 2]);
    try testing.expect(std.mem.startsWith(u8, argv[argv.len - 1], ". '"));
    try testing.expect(std.mem.indexOf(u8, argv[argv.len - 1], "ghostty.ps1") != null);
    try testing.expect(env.get("GHOSTTY_POWERSHELL") != null);
}

test "powershell: non-interactive invocations are left alone" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .powershell);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    for ([_][:0]const u8{
        "pwsh -Command echo hi",
        "pwsh -File script.ps1",
        "powershell.exe -c whoami",
    }) |cmd| {
        try testing.expect(try setup(
            alloc,
            res.path,
            .{ .shell = cmd },
            &env,
            null,
        ) == null);
    }
}

test "nushell" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .nushell);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const command = try setupNushell(alloc, .{ .shell = "nu" }, res.path, &env);
    try testing.expectEqualStrings("nu --execute 'use ghostty *'", command.?.shell);

    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    try testing.expectEqualStrings(
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration", .{res.path}),
        env.get("GHOSTTY_SHELL_INTEGRATION_XDG_DIR").?,
    );
    try testing.expectStringStartsWith(
        env.get("XDG_DATA_DIRS").?,
        try std.fmt.bufPrint(&path_buf, "{s}/shell-integration", .{res.path}),
    );
}

test "nushell: a direct command keeps its argv boundaries (T862)" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .nushell);
    defer res.deinit();

    var env = EnvMap.init(alloc);
    defer env.deinit();

    const spaced = "C:\\Program Files\\nu\\bin\\nu.exe";
    const argv: []const [:0]const u8 = &.{spaced};
    const command = try setupNushell(alloc, .{ .direct = argv }, res.path, &env);
    try testing.expect(command.? == .direct);
    try testing.expectEqual(@as(usize, 3), command.?.direct.len);
    try testing.expectEqualStrings(spaced, command.?.direct[0]);
    // The `--execute` VALUE is a separate argv element with no quote
    // characters in it: `.direct` has no shell-words parse to strip them.
    try testing.expectEqualStrings("--execute", command.?.direct[1]);
    try testing.expectEqualStrings("use ghostty *", command.?.direct[2]);
}

test "nushell: unsupported options" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(alloc, .nushell);
    defer res.deinit();

    const cmdlines = [_][:0]const u8{
        "nu --command exit",
        "nu --lsp",
        "nu -c script.sh",
        "nu -ic script.sh",
    };

    for (cmdlines) |cmdline| {
        var env = EnvMap.init(alloc);
        defer env.deinit();

        try testing.expect(try setupNushell(alloc, .{ .shell = cmdline }, res.path, &env) == null);
        try testing.expect(env.get("XDG_DATA_DIRS") != null);
        try testing.expect(env.get("GHOSTTY_SHELL_INTEGRATION_XDG_DIR") != null);
    }
}

test "nushell: missing resources" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();

    const resources_dir = try tmp_dir.dir.realpathAlloc(alloc, ".");
    defer alloc.free(resources_dir);

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try testing.expect(try setupNushell(alloc, .{ .shell = "nu" }, resources_dir, &env) == null);
    try testing.expectEqual(0, env.count());
}

/// Setup the zsh automatic shell integration. This works by setting
/// ZDOTDIR to our resources dir so that zsh will load our config. This
/// config then loads the true user config.
fn setupZsh(
    alloc: Allocator,
    command: config.Command,
    resource_dir: []const u8,
    env: *EnvMap,
) !?config.Command {
    // Preserve an existing ZDOTDIR value. We're about to overwrite it.
    if (env.get("ZDOTDIR")) |old| {
        try env.put("GHOSTTY_ZSH_ZDOTDIR", old);
    }

    // Set our new ZDOTDIR to point to our shell resource directory.
    var path_buf: [std.fs.max_path_bytes]u8 = undefined;
    const integ_path = try std.fmt.bufPrint(
        &path_buf,
        "{s}/shell-integration/zsh",
        .{resource_dir},
    );
    var integ_dir = std.fs.openDirAbsolute(integ_path, .{}) catch |err| {
        log.warn("unable to open {s}: {}", .{ integ_path, err });
        return null;
    };
    integ_dir.close();
    try env.put("ZDOTDIR", integ_path);

    return try command.clone(alloc);
}

test "zsh" {
    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(testing.allocator, .zsh);
    defer res.deinit();

    var env = EnvMap.init(testing.allocator);
    defer env.deinit();

    const command = try setupZsh(alloc, .{ .shell = "zsh" }, res.path, &env);
    try testing.expectEqualStrings("zsh", command.?.shell);
    try testing.expectEqualStrings(res.shell_path, env.get("ZDOTDIR").?);
    try testing.expect(env.get("GHOSTTY_ZSH_ZDOTDIR") == null);
}

test "zsh: ZDOTDIR" {
    const testing = std.testing;

    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var res: TmpResourcesDir = try .init(testing.allocator, .zsh);
    defer res.deinit();

    var env = EnvMap.init(testing.allocator);
    defer env.deinit();

    try env.put("ZDOTDIR", "$HOME/.config/zsh");

    const command = try setupZsh(alloc, .{ .shell = "zsh" }, res.path, &env);
    try testing.expectEqualStrings("zsh", command.?.shell);
    try testing.expectEqualStrings(res.shell_path, env.get("ZDOTDIR").?);
    try testing.expectEqualStrings("$HOME/.config/zsh", env.get("GHOSTTY_ZSH_ZDOTDIR").?);
}

test "zsh: missing resources" {
    const testing = std.testing;
    var arena = ArenaAllocator.init(testing.allocator);
    defer arena.deinit();
    const alloc = arena.allocator();

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();

    const resources_dir = try tmp_dir.dir.realpathAlloc(alloc, ".");
    defer alloc.free(resources_dir);

    var env = EnvMap.init(alloc);
    defer env.deinit();

    try testing.expect(try setupZsh(alloc, .{ .shell = "zsh" }, resources_dir, &env) == null);
    try testing.expectEqual(0, env.count());
}

/// Test helper that creates a temporary resources directory with shell integration paths.
const TmpResourcesDir = struct {
    allocator: Allocator,
    tmp_dir: std.testing.TmpDir,
    path: []const u8,
    shell_path: []const u8,

    fn init(allocator: Allocator, shell: Shell) !TmpResourcesDir {
        var tmp_dir = std.testing.tmpDir(.{});
        errdefer tmp_dir.cleanup();

        var path_buf: [std.fs.max_path_bytes]u8 = undefined;
        const relative_shell_path = try std.fmt.bufPrint(
            &path_buf,
            "shell-integration/{s}",
            .{@tagName(shell)},
        );
        try tmp_dir.dir.makePath(relative_shell_path);

        const path = try tmp_dir.dir.realpathAlloc(allocator, ".");
        errdefer allocator.free(path);

        const shell_path = try std.fmt.allocPrint(
            allocator,
            "{s}/{s}",
            .{ path, relative_shell_path },
        );
        errdefer allocator.free(shell_path);

        switch (shell) {
            .bash => try tmp_dir.dir.writeFile(.{
                .sub_path = "shell-integration/bash/ghostty.bash",
                .data = "",
            }),
            .powershell => try tmp_dir.dir.writeFile(.{
                .sub_path = "shell-integration/powershell/ghostty.ps1",
                .data = "",
            }),
            else => {},
        }

        return .{
            .allocator = allocator,
            .tmp_dir = tmp_dir,
            .path = path,
            .shell_path = shell_path,
        };
    }

    fn deinit(self: *TmpResourcesDir) void {
        self.allocator.free(self.shell_path);
        self.allocator.free(self.path);
        self.tmp_dir.cleanup();
    }
};
